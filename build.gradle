buildscript {
    repositories {
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
    }
}

plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
}

repositories {
    maven {
        url 'https://masa.dy.fi/maven/'
    }
    maven {
        url 'https://www.cursemaven.com/'
    }
    maven {
        url 'https://api.modrinth.com/maven'
    }
}

dependencies {
    // Minecraft, Fabric and mappings versions
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.mappings_version}:v2")
    modApi("net.fabricmc:fabric-loader:${project.fabric_loader_version}")
    // Malilib
    modApi("fi.dy.masa.malilib:malilib-fabric-${project.minecraft_version_masa}:${project.malilib_version}")
    // ModMenu
    modCompileOnly("maven.modrinth:modmenu:" + project.mod_menu_version)
    // Optional mod compatibility Library
    modCompileOnly("curse.maven:authme-356643:3073169")
    modCompileOnly("curse.maven:litematica-308892:3379278")
    modCompileOnly("curse.maven:reauth-403928:3105780")
}

minecraft {
}

loom {
    customManifest = "https://gist.githubusercontent.com/modmuss50/6e00baf80dcbaa42f3a2fc846b290128/raw/f035ef8f75164f56ec6352809a34841d326bae1c/1_18_experimental-snapshot-1.json"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

group = project.group + "." + project.mod_id
archivesBaseName = project.mod_file_name + '-mc' + project.minecraft_version_out
version = project.mod_version_detail

processResources
{
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        include "fabric.mod.json"
        expand "mod_version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(jar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
    }
}

