plugins {
    id("maven-publish")
    id("fabric-loom").version("1.0-SNAPSHOT").apply(false)
    // https://github.com/Juuxel/LoomQuiltflower
    id("io.github.juuxel.loom-quiltflower").version("1.8.0").apply(false)
    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id("com.replaymod.preprocess").version("b7207cffa9")
    id("org.ajoberstar.grgit").version("5.0.0")
    id("net.kyori.blossom").version("1.3.1").apply(false)
}

preprocess {
    def mc114 = createNode("1.14.4", 1_14_04, "mojang")
    def mc115 = createNode("1.15.2", 1_15_02, "mojang")
    def mc116 = createNode("1.16.5", 1_16_05, "mojang")
    def mc117 = createNode("1.17.1", 1_17_01, "mojang")
    def mc118 = createNode("1.18.2", 1_18_02, "mojang")
    def mc119 = createNode("1.19.3", 1_19_03, "mojang")

    mc114.link(mc115, null)
    mc115.link(mc116, null)
    mc116.link(mc117, null)
    mc117.link(mc118, null)
    mc118.link(mc119, file("versions/mapping-1.18.2-1.19.3.txt"))
}

String getVersionGit(List paths) {
    if (grgit == null) {
        return "nogit"
    }
    List latestCommits = paths.isEmpty() ? grgit.log(maxCommits: 1) : grgit.log(paths: paths, maxCommits: 1)
    return latestCommits.isEmpty() ? "uncommited" : "${latestCommits.get(0).id.substring(0, 7)}"
}

String getBuildNumber() {
    Map<String, String> ENV = System.getenv()
    return ENV.GITHUB_RUN_NUMBER ? ENV.GITHUB_RUN_NUMBER : Integer.MAX_VALUE
}

String getVersionType() {
    Map<String, String> ENV = System.getenv()
    switch (ENV.BUILD_TYPE) {
        case "RELEASE":
            return "stable"
        case "BETA":
            return "beta"
        default:
            return "dev"
    }
}

int getVersionPatch(List paths) {
    if (grgit == null) {
        return 0
    }
    List latestCommits = paths.isEmpty() ? grgit.log() : grgit.log(paths: paths)
    return latestCommits.size()
}

setVersion("${project.mod_version}.${project.getVersionPatch([])}+${project.getVersionGit([])}-${project.getVersionType()}")